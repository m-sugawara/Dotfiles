# -------------------------------------
# Ctl+Yでスペースまでの内容を消す
# -------------------------------------

function _kill-backward-blank-word() {
  zle set-mark-command
  zle vi-backward-blank-word
  zle kill-region
}
zle -N _kill-backward-blank-word
bindkey '^Y' _kill-backward-blank-word

# -------------------------------------
# gitの差分を見る
# -------------------------------------
__git-inside-work-tree() {
    [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == true ]]
}

__git-status() {
    __git-inside-work-tree || return
    print "$(git status --short --porcelain)"
}

# 分岐元のブランチと現在のブランチのHEADを比較して変更のあったファイルリストを返す
__git-changed-list() {
    __git-inside-work-tree || return
    print $(git diff --name-only origin/HEAD...HEAD)
}

# 以降はgit statusの結果で各状態のファイルリストを返す関数
__git-modified-list() {
    __git-inside-work-tree || return
    local -a git_status_res

    git_status_res=(${(@f)"$(__git-status)"})
    print ${(R)${(M)git_status_res:#?M*}#?M[[:space:]]}
}

__git-untracked-list() {
    __git-inside-work-tree || return
    local -a git_status_res

    git_status_res=(${(@f)"$(__git-status)"})
    print ${(R)${(M)git_status_res:#\?\?*}#\?\?[[:space:]]}
}

__git-staged-list() {
    __git-inside-work-tree || return
    local -a git_status_res

    git_status_res=(${(@f)"$(__git-status)"})
    print ${(R)${(M)git_status_res:#M?*}#M?[[:space:]]}
}

__git-both-modified-list() {
    __git-inside-work-tree || return
    local -a git_status_res

    git_status_res=(${(@f)"$(__git-status)"})
    print ${(R)${(M)git_status_res:#UU*}#UU[[:space:]]}
}

# コマンド割り当て
git-diff-files() {
    git diff $*
}

__git-modified-files() {
    compadd $(__git-modified-list)
}

_git-diff-files() {
    _arguments '*: :__git-modified_files'
}

compdef _git-diff-files git-diff-files
